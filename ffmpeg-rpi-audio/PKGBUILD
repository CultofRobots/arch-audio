# Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://github.com/CultofRobots
# Based on ffmpeg-headless from AUR made by the following people
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Paul Mattal <paul@archlinux.org>

_pkgname=ffmpeg
pkgname=ffmpeg-rpi-audio
pkgver=4.1.1
pkgrel=1
epoch=1
pkgdesc='The FFmpeg minimal audio toolkit optimized for headless raspberry pi systems'
arch=('armv7h')
url='http://ffmpeg.org/'
license=('GPL3')
depends=(
  bzip2
  glibc
  gnutls
  xz
  zlib
  lame
  libvorbis
  libsoxr
  opus
  rtmpdump
)
makedepends=(
  git
  ladspa
  yasm
)
optdepends=('ladspa: LADSPA filters')
provides=(
  libavcodec.so
  libavdevice.so
  libavfilter.so
  libavformat.so
  libavutil.so
  libpostproc.so
  libswresample.so
  ffmpeg
)
conflicts=('ffmpeg')
replaces=(
  ffmpeg
  ffmpeg-archphile
)
source=("git+https://git.ffmpeg.org/ffmpeg.git#tag=n${pkgver}")
sha256sums=('SKIP')

build() {
  cd ${_pkgname}

  [[ $CARCH == "armv7h" || $CARCH == "aarch64" ]] && CONFIG='--host-cflags="-fPIC"'

  ./configure \
    --prefix='/usr' \
    --disable-htmlpages \
    --disable-podpages \
    --disable-txtpages \
    --disable-swscale \
    --disable-everything \
    --enable-gpl \
    --enable-nonfree \
    --enable-shared \
    --enable-protocols \
    --enable-libmp3lame \
    --enable-libopus \
    --enable-librtmp \
    --enable-libsoxr \
    --enable-libvorbis \
    --enable-encoder=flac \
    --enable-decoder=aac,ac3,alac,flac,mp3,opus,vorbis \
    --enable-decoder=dsd_lsbf,dsd_lsbf_planar,dsd_msbf,dsd_msbf_planar,dst,dts,dolby_e \
    --enable-decoder=pcm_alaw,pcm_bluray,pcm_dvd,pcm_lxf,pcm_mulaw,pcm_zork \
    --enable-decoder=pcm_f16le,pcm_f24le,pcm_f32be,pcm_f32le,pcm_f64be,pcm_f64le \
    --enable-decoder=pcm_s8,pcm_s8_planar \
    --enable-decoder=pcm_s16be,pcm_s16be_planar,pcm_s16le,pcm_s16le_planar \
    --enable-decoder=pcm_s24be,pcm_s24daud,pcm_s24le,pcm_s24le_planar \
    --enable-decoder=pcm_s32be,pcm_s32le,pcm_s32le_planar \
    --enable-decoder=pcm_s64be,pcm_s64le \
    --enable-decoder=pcm_u8 \
    --enable-decoder=pcm_u16be,pcm_u16le \
    --enable-decoder=pcm_u24be,pcm_u24le \
    --enable-decoder=pcm_u32be,pcm_u32le \
    --enable-demuxer=aac,ac3,dts,flac,mp3 \
    --enable-demuxer=pcm_alaw,pcm_mulaw \
    --enable-demuxer=pcm_f32be,pcm_f32le,pcm_f64be,pcm_f64le \
    --enable-demuxer=pcm_s16be,pcm_s16le,pcm_s24be,pcm_s24le,pcm_s32be,pcm_s32le \
    --enable-demuxer=pcm_u16be,pcm_u16le,pcm_u24be,pcm_u24le,pcm_u32be,pcm_u32le \
    --enable-parser=aac,ac3,flac,opus,vorbis

  make
}

package() {
  cd ${_pkgname}

  make DESTDIR="${pkgdir}" install install-man
}
