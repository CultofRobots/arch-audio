  # Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://github.com/CultofRobots
# Based on ffmpeg-headless from AUR
# Contributor: Maxime Gauduin <alucryd [at] archlinux [dot] org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski [at] archlinux [dot] org>
# Contributor: Ionut Biru <ibiru [at] archlinux [dot] org>
# Contributor: Tom Newsom <Jeepster [at] gmx.co.uk>
# Contributor: Paul Mattal <paul [at] archlinux [dot] org>

_pkgname=ffmpeg
pkgname=ffmpeg-rpi-audio
pkgver=6.1
pkgrel=1
epoch=1
pkgdesc='The FFmpeg minimal audio toolkit optimized for headless raspberry pi systems'
arch=('x86_64' 'armv7h' 'aarch64')
url='http://ffmpeg.org/'
license=('GPL3')
depends=(
  bzip2
  gnutls
  lame
  libbs2b.so
  libsoxr
  libvorbis
  libvorbisenc.so
  opus
  xz
  zlib
)
makedepends=(
  clang
  git
  ladspa
)
optdepends=('ladspa: LADSPA filters')
provides=(
  libavcodec.so
  libavdevice.so
  libavfilter.so
  libavformat.so
  libavutil.so
  libpostproc.so
  libswresample.so
  ffmpeg
  ffmpeg-archphile
)
conflicts=('ffmpeg')
replaces=(
  ffmpeg
  ffmpeg-archphile
)
_tag=3cdfac27d3ea06f8719faed48b4ae2e75e94a463
source=(
  git+https://git.ffmpeg.org/ffmpeg.git?signed#tag=${_tag}
)
b2sums=('SKIP')
validpgpkeys=(DD1EC9E8DE085C629B3E1846B18E8928B3948D64) # Michael Niedermayer <michael@niedermayer.cc>

prepare() {
  cd ${_pkgname}
  # FS#79281: fix assembling with binutil as >= 2.41
  git cherry-pick -n effadce6c756247ea8bae32dc13bb3e6f464f0eb
  # FS#77813: fix playing ogg files with mplayer
  git cherry-pick -n cbcc817353a019da4332ad43deb7bbc4e695d02a
}

pkgver() {
  cd ${_pkgname}

  git describe --tags | sed 's/^n//'
}

build() {
  cd ${_pkgname}

  [[ $CARCH == "armv7h" || $CARCH == "aarch64" ]] && CONFIG='--host-cflags="-fPIC"'

  ./configure \
    --prefix=/usr \
    --disable-debug \
    --disable-static \
    --disable-stripping \
    --disable-htmlpages \
    --disable-podpages \
    --disable-txtpages \
    --disable-swscale \
    --disable-everything \
    --enable-decoder=aac,ac3,alac,bs2b,flac,mp3,opus,vorbis \
    --enable-decoder=dsd_lsbf,dsd_lsbf_planar,dsd_msbf,dsd_msbf_planar,dst,dts,dolby_e \
    --enable-decoder=pcm_alaw,pcm_bluray,pcm_dvd,pcm_lxf,pcm_mulaw,pcm_zork \
    --enable-decoder=pcm_f16le,pcm_f24le,pcm_f32be,pcm_f32le,pcm_f64be,pcm_f64le \
    --enable-decoder=pcm_s8,pcm_s8_planar \
    --enable-decoder=pcm_s16be,pcm_s16be_planar,pcm_s16le,pcm_s16le_planar \
    --enable-decoder=pcm_s24be,pcm_s24daud,pcm_s24le,pcm_s24le_planar \
    --enable-decoder=pcm_s32be,pcm_s32le,pcm_s32le_planar \
    --enable-decoder=pcm_s64be,pcm_s64le \
    --enable-decoder=pcm_u8 \
    --enable-decoder=pcm_u16be,pcm_u16le \
    --enable-decoder=pcm_u24be,pcm_u24le \
    --enable-decoder=pcm_u32be,pcm_u32le \
    --enable-demuxer=aac,ac3,dts,flac,mp3 \
    --enable-demuxer=pcm_alaw,pcm_mulaw \
    --enable-demuxer=pcm_f32be,pcm_f32le,pcm_f64be,pcm_f64le \
    --enable-demuxer=pcm_s16be,pcm_s16le,pcm_s24be,pcm_s24le,pcm_s32be,pcm_s32le \
    --enable-demuxer=pcm_u16be,pcm_u16le,pcm_u24be,pcm_u24le,pcm_u32be,pcm_u32le \
    --enable-encoder=flac,libvorbis \
    --enable-filter=bs2b \
    --enable-gpl \
    --enable-libbs2b \
    --enable-libmp3lame \
    --enable-libopus \
    --enable-libsoxr \
    --enable-libvorbis \
    --enable-lto \
    --enable-parser=aac,ac3,flac,opus,vorbis \
    --enable-shared
  make
  make doc/ff{mpeg,play}.1

}

package() {
  make DESTDIR="${pkgdir}" -C ffmpeg install install-man
}
