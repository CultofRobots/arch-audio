  # Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://github.com/CultofRobots
# Based on ffmpeg-headless from AUR
# Contributor: Maxime Gauduin <alucryd [at] archlinux [dot] org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski [at] archlinux [dot] org>
# Contributor: Ionut Biru <ibiru [at] archlinux [dot] org>
# Contributor: Tom Newsom <Jeepster [at] gmx.co.uk>
# Contributor: Paul Mattal <paul [at] archlinux [dot] org>

_pkgname=ffmpeg
pkgname=ffmpeg-rpi-audio
pkgver=8.0.1
pkgrel=1
epoch=1
pkgdesc='The FFmpeg minimal audio toolkit optimized for headless raspberry pi systems'
arch=('x86_64' 'armv7h' 'aarch64')
url='http://ffmpeg.org/'
license=('GPL3')
depends=(
  bzip2
  gnutls
  lame
  libbs2b
  libsoxr
  libvorbis
  mbedtls2
  opus
  xz
  zlib
)
makedepends=(
  clang
  git
  ladspa
)
optdepends=('ladspa: LADSPA filters')
provides=(
  libavcodec.so
  libavdevice.so
  libavfilter.so
  libavformat.so
  libavutil.so
  libswresample.so
  ffmpeg
)
conflicts=('ffmpeg')
replaces=(
  ffmpeg
  ffmpeg-archphile
)
_tag=d22ecc4f6f3fca77b3e71b18641ceddb25973e97
source=(
  git+https://git.ffmpeg.org/ffmpeg.git?signed#tag=${_tag}
)
b2sums=('0796d77c58d5db487ccda15454dadf129a6e0ae2a5a9cec562c86f2050b1e9314164c7f2dbf0a6c8cdae37dfd5e28f494d5ac248e970bc7f01b097100971fe8a')
validpgpkeys=(DD1EC9E8DE085C629B3E1846B18E8928B3948D64) # Michael Niedermayer <michael@niedermayer.cc>

prepare() {
  cd ${_pkgname}
}

pkgver() {
  cd ${_pkgname}
  git describe --tags | sed 's/^n//'
}

build() {
  export PKG_CONFIG_PATH='/usr/lib/mbedtls2/pkgconfig'
  cd ${_pkgname}

  [[ $CARCH == "armv7h" || $CARCH == "aarch64" ]] && CONFIG='--host-cflags="-fPIC"'

  ./configure \
    --prefix=/usr \
    --disable-debug \
    --disable-static \
    --disable-stripping \
    --disable-htmlpages \
    --disable-podpages \
    --disable-txtpages \
    --disable-swscale \
    --disable-everything \
    --enable-decoder=aac,ac3,alac,bs2b,flac,mp3,opus,vorbis \
    --enable-decoder=dsd_lsbf,dsd_lsbf_planar,dsd_msbf,dsd_msbf_planar,dst,dts,dolby_e \
    --enable-decoder=pcm_alaw,pcm_bluray,pcm_dvd,pcm_lxf,pcm_mulaw,pcm_zork \
    --enable-decoder=pcm_f16le,pcm_f24le,pcm_f32be,pcm_f32le,pcm_f64be,pcm_f64le \
    --enable-decoder=pcm_s8,pcm_s8_planar \
    --enable-decoder=pcm_s16be,pcm_s16be_planar,pcm_s16le,pcm_s16le_planar \
    --enable-decoder=pcm_s24be,pcm_s24daud,pcm_s24le,pcm_s24le_planar \
    --enable-decoder=pcm_s32be,pcm_s32le,pcm_s32le_planar \
    --enable-decoder=pcm_s64be,pcm_s64le \
    --enable-decoder=pcm_u8 \
    --enable-decoder=pcm_u16be,pcm_u16le \
    --enable-decoder=pcm_u24be,pcm_u24le \
    --enable-decoder=pcm_u32be,pcm_u32le \
    --enable-demuxer=aac,ac3,dts,flac,mov,mp3 \
    --enable-demuxer=pcm_alaw,pcm_mulaw \
    --enable-demuxer=pcm_f32be,pcm_f32le,pcm_f64be,pcm_f64le \
    --enable-demuxer=pcm_s16be,pcm_s16le,pcm_s24be,pcm_s24le,pcm_s32be,pcm_s32le \
    --enable-demuxer=pcm_u16be,pcm_u16le,pcm_u24be,pcm_u24le,pcm_u32be,pcm_u32le \
    --enable-encoder=flac,libvorbis \
    --enable-filter=bs2b \
    --enable-gpl \
    --enable-libbs2b \
    --enable-libmp3lame \
    --enable-libopus \
    --enable-libsoxr \
    --enable-libvorbis \
    --enable-lto \
    --enable-parser=aac,ac3,flac,opus,vorbis \
    --enable-shared \
    --enable-version3
  make
  make doc/ff{mpeg,play}.1

}

package() {
  depends+=(
    libbs2b.so
    libvorbisenc.so
    libvorbis.so    
  )

  make DESTDIR="${pkgdir}" -C ffmpeg install install-man
}
