# Maintainer: K. Loz <cultofrobots [at] protonmail [dot] com>
# Website: https://github.com/CultofRobots
# Based on jcorporation's myMPD mkrelease.sh 
# Website: https://github.com/jcorporation/myMPD
# PKGBUILD Based on mympd-archphile by Mike Andonov <info [at] archphile [dot] org>
# Website: https://archphile.org/

pkgname=mympd-rpi
_pkgname=myMPD
pkgver=4.1.2
pkgrel=1
pkgdesc="A standalone MPD Web GUI based on YMPD - Default port set to 80"
arch=('armv7h' 'aarch64')
url="http://github.org/jcorporation/myMPD"
license=('GPL')
depends=('libmpdclient' 'openssl')
makedepends=('cmake' 'git')
optdepends=()
provides=()
conflicts=(mympd)
replaces=(mympd)
install=mympd-rpi.install
source=("https://github.com/jcorporation/${_pkgname}/archive/v${pkgver}.tar.gz"
        'mympd-rpi.install')
sha256sums=('2f5802597e53569a89636836f9ba47934fe6d32720a938e12e8686f0c76462da'
            'ef1f79e4b4eccbd0d94b6b3de76df1a2940f43ae1fe58fc9bd1bafa7196ad11b')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  
  [ -d release ] || mkdir release
  cd release
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_BUILD_TYPE=RELEASE ..
  make
}


package() {
  cd "${srcdir}/${_pkgname}-${pkgver}/release"
  make DESTDIR="$pkgdir/" install

  ### DON'T OVERWRITE CURRENT CONFIG ###
  mv "${pkgdir}/etc/mympd/mympd.conf" "${pkgdir}/etc/mympd/mympd.conf.dist"

  install -Dm644  "${srcdir}/${_pkgname}-${pkgver}/contrib/mympd.service" "$pkgdir/usr/lib/systemd/system/mympd.service"

  echo "Replacing javascript and stylesheets with minified files"
  sed -e 's/mympd\.css/mympd\.min\.css/' -e 's/mympd\.js/mympd\.min\.js/' ${srcdir}/${_pkgname}-${pkgver}/htdocs/index.html > ${srcdir}/${_pkgname}-${pkgver}/dist/htdocs/index.html
  sed -e 's/mympd\.css/mympd\.min\.css/' -e 's/player\.js/player\.min\.js/' ${srcdir}/${_pkgname}-${pkgver}/htdocs/player.html > ${srcdir}/${_pkgname}-${pkgver}/dist/htdocs/player.html
  sed -i -e 's/mympd\.css/mympd\.min\.css/' -e 's/mympd\.js/mympd\.min\.js/' -e 's/player\.js/player\.min\.js/' ${srcdir}/${_pkgname}-${pkgver}/dist/htdocs/sw.min.js
  sed -i -e 's/\/sw\.js/\/sw\.min\.js/' ${srcdir}/${_pkgname}-${pkgver}/dist/htdocs/js/mympd.min.js

  echo "Minifying html"
  perl -i -pe 's/^\s*//gm; s/\s*$//gm' ${srcdir}/${_pkgname}-${pkgver}/dist/htdocs/index.html
  perl -i -pe 's/^\s*//gm; s/\s*$//gm' ${srcdir}/${_pkgname}-${pkgver}/dist/htdocs/player.html
}
