# Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://cultofrobots.net
# Based on Mike Andonov's upmpdcli-archphile from http://archphile.org
# Contributor: Mike Andonov <info [at] archphile [dot] org>
# Contributor: Mansour Behabadi <mansour [at] oxplot [dot] com>
# Contributor: Tilman Vatteroth <tilman.vatteroth [at] udo [dot] edu>

pkgname=upmpdcli
pkgver=1.5.13
pkgrel=2
pkgdesc="A UPnP Media Renderer front-end for the Music Player Daemon (MPD)"
arch=(x86_64 armv7h aarch64)
url="http://www.lesbonscomptes.com/upmpdcli/"
license=(GPL2)
depends=(python libmpdclient libupnpp libmicrohttpd jsoncpp)
makedepends=(python-setuptools python-requests python-bottle python-mutagen aspell-en id3lib python-waitress sqlite)
optdepends=(aspell-en id3lib sqlite
            'python-requests: enable search'
            'python-bottle: enable uprcl media server'
            'python-mutagen: enable uprcl media server' 
            'python-waitress: enable uprcl media server')
install=upmpdcli.install
source=("http://www.lesbonscomptes.com/upmpdcli/downloads/${pkgname}-${pkgver}.tar.gz"
        'upmpdcli.install'
        'upmpdcli.service'
        'upmpdcli.conf'
        'archphile.png')
sha256sums=('c7726f0d4062ae253159102e499759bd513d3c68bfe16c44997ee3fba35509f8'
            '26e445afa98f8177ea5202717fe3ac1cda6bb3d4a234a943d918fde4b04bc697'
            '7144c7f96942c758ffae26d64dc83bf7c2ba142c69d7f83afea5b2fe7ae886f8'
            '4ef07303a1aef48eb26c938951bc0f699d46c1600a5c0a877f4f984e701640cb'
            '3e6da9fc442b7bb798b6f3dd3b70ee854f1bbd177375ef47b302eeffdb0d6fd1')
backup=('etc/upmpdcli.conf')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --sysconfdir=/etc --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 systemd/upmpdcli.service ${pkgdir}/usr/lib/systemd/system/upmpdcli.service
  sed '/\[Service\]/a User=upmpdcli' -i ${pkgdir}/usr/lib/systemd/system/upmpdcli.service
  sed -i '/^After/ s/$/ mpd.service/' ${pkgdir}/usr/lib/systemd/system/upmpdcli.service
  install -Dm644 $srcdir/upmpdcli.service ${pkgdir}/usr/lib/systemd/user/upmpdcli.service
  mkdir -p ${pkgdir}/etc/
  cp ../../upmpdcli.conf ${pkgdir}/etc/
  chmod 644 ${pkgdir}/etc/upmpdcli.conf
  cp ../../archphile.png ${pkgdir}/usr/share/upmpdcli/
}
