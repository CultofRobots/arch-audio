# Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://cultofrobots.net
# Based on Mike Andonov's upmpdcli-archphile from http://archphile.org
# Contributor: Mike Andonov <info [at] archphile [dot] org>
# Contributor: Mansour Behabadi <mansour [at] oxplot [dot] com>
# Contributor: Tilman Vatteroth <tilman.vatteroth [at] udo [dot] edu>

pkgname=upmpdcli
pkgver=1.5.11
pkgrel=1
pkgdesc="A UPnP Media Renderer front-end for the Music Player Daemon (MPD)"
arch=('x86_64' 'armv7h' 'aarch64')
url="http://www.lesbonscomptes.com/upmpdcli/"
license=('GPL2')
depends=('python' 'libmpdclient' 'libupnpp>=0.17.0' 'libmicrohttpd' 'jsoncpp' 'python-requests')
makedepends=('python-setuptools' 'libmpdclient' 'libupnpp>=0.17.0' 'libmicrohttpd' 'jsoncpp' 'python-requests' 'recoll' 'python-bottle' 'python-mutagen' 'aspell-en' 'id3lib' 'python-waitress' 'sqlite')
optdepends=('python2: OpenHome Radio Service' 'recoll: enable search' 'python-bottle: enable uprcl media server' 'python-mutagen: enable uprcl media server' 'mutagen: enable uprcl media server' 'aspell-en' 'id3lib' 'python-waitress: enable uprcl media server' 'sqlite')
install=upmpdcli.install
source=("http://www.lesbonscomptes.com/upmpdcli/downloads/${pkgname}-${pkgver}.tar.gz"
        'upmpdcli.service'
        'upmpdcli.conf'
        'archphile.png')
sha256sums=('7c8c6ab866114699405223c60457448dcce35fc13e1e374f68b60eefc55f4f04'
            '7144c7f96942c758ffae26d64dc83bf7c2ba142c69d7f83afea5b2fe7ae886f8'
            '4ef07303a1aef48eb26c938951bc0f699d46c1600a5c0a877f4f984e701640cb'
            '3e6da9fc442b7bb798b6f3dd3b70ee854f1bbd177375ef47b302eeffdb0d6fd1')
backup=('etc/upmpdcli.conf')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --sysconfdir=/etc --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 systemd/upmpdcli.service ${pkgdir}/usr/lib/systemd/system/upmpdcli.service
  sed '/\[Service\]/a User=upmpdcli' -i ${pkgdir}/usr/lib/systemd/system/upmpdcli.service
  sed -i '/^After/ s/$/ mpd.service/' ${pkgdir}/usr/lib/systemd/system/upmpdcli.service
  install -Dm644 $srcdir/upmpdcli.service ${pkgdir}/usr/lib/systemd/user/upmpdcli.service
  mkdir -p ${pkgdir}/etc/
  cp ../../upmpdcli.conf ${pkgdir}/etc/
  chmod 644 ${pkgdir}/etc/upmpdcli.conf
  cp ../../archphile.png ${pkgdir}/usr/share/upmpdcli/
}
