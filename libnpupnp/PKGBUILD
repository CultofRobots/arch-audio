# Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://github.com/CultofRobots
# Based on Mike Andonov's libnpupnp-archphile from http://archphile.org
# Contributor: Mike Andonov <info [at] archphile [dot] org>
# Contributor: Fabio Loli <loli_fabio [at] protonmail [dot] com>
# Contributor: Tristelune <tristelune [at] archlinux [dot] info>

pkgname=libnpupnp
pkgver=5.0.1
pkgrel=1
pkgdesc="an UPnP library derived from the venerable pupnp"
url="http://www.lesbonscomptes.com/upplay/downloads.html"
arch=('x86_64' 'armv7h' 'aarch64')
license=(LGPLv2.1)
depends=(
  'curl'
  'expat'
  'libmicrohttpd'
)
source=("http://www.lesbonscomptes.com/upmpdcli/downloads/${pkgname}-${pkgver}.tar.gz")
sha256sums=('3fbc2a63e12c0db2235926017e82e78037278a3ec7f97e1a4e160a5db3576a1c')

prepare() {
  cd "${pkgname}-${pkgver}"
  ./autogen.sh
}

build(){
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package(){
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
