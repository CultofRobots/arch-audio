# Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://github.com/CultofRobots
# Based on Mike Andonov's libnpupnp-archphile from http://archphile.org
# Contributor: Mike Andonov <info [at] archphile [dot] org>
# Contributor: Fabio Loli <loli_fabio [at] protonmail [dot] com>
# Contributor: Tristelune <tristelune [at] archlinux [dot] info>

pkgname=libnpupnp
pkgver=6.0.1
pkgrel=1
pkgdesc="an UPnP library derived from the venerable pupnp"
url="http://www.lesbonscomptes.com/upplay/downloads.html"
arch=('x86_64' 'armv7h' 'aarch64')
license=(LGPLv2.1)
depends=(
  'glibc'
  'gcc-libs'
  'curl'
  'expat'
  'libmicrohttpd'
)
provides=(libnpupnp.so)
source=("http://www.lesbonscomptes.com/upmpdcli/downloads/${pkgname}-${pkgver}.tar.gz")
sha256sums=('eaf7a1695d577175ea34754c2387a5dca636eafa63462c5b776d92adff3415fe')

prepare() {
  cd "${pkgname}-${pkgver}"
  ./autogen.sh
}

build(){
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package(){
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
