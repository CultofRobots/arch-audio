# Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://github.com/CultofRobots
# Based on PKGBUILD from AUR
# Contributor: jason ryan <jasonwryan [at] gmail [dot] com>
# Contributor: Bart≈Çomiej Piotrowski <nospam [at] bpiotrowwski [dot] pl>
# Contributor: Thomas Dziedzic <gostrc [at] gmail [dot] com>
# Contributor: evr <evanroman [at] gmail [dot] com>
# Contributor: Luiz Ribeiro <luizribeiro [at] gmail [dot] com>

pkgname=mpdscribble
pkgver=0.23
pkgrel=1
pkgdesc='MPD client which submits track info to {Libre,Last}.fm'
url="http://www.musicpd.org/"
arch=('x86_64' 'armv7h' 'aarch64')
license=('GPL2')
depends=('boost' 'libmpdclient' 'curl' 'libgcrypt')
makedepends=('meson' 'ninja')
backup=("etc/${pkgname}.conf")
source=("https://www.musicpd.org/download/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.sig}
        "${pkgname}.conf"
        "${pkgname}.sysusers"
        "${pkgname}.tmpfiles")
sha512sums=('f924076a8aa201f67ba81e72a44d3dd1c31618bd87e4f75f6b63f7b3814fa08f6ef4f7964b1d518c5f8b68a1f8ba88f867c08ed8b0387157eec8e9f97eb0b825'
            'SKIP'
            '1075e5475484256e09760bad7e72233a97b0b2502f76836cf910ff2977a009799c341a63bb4dc7d82f7e350d5caf2e36a7360ea39bb7468187a5fac5cb3608cc'
            '0f5b302ef3124a061a84fdd3dd8dd1a08a31bc99703d7dc32413fe499ea9613babf915f89926578019d8b6a2414e772022203de962bf971de8081af5b07d09c7'
            '67d6f5bfd70f3d949bd3d04bee93e6953ca3b1e50e71b2bc94f359c8abf385537424c3c386ba1d6cbe808b6e4498c4cfd191251076e4df0394a66bcc57ef2c48')
b2sums=('47c59a4b2936b7990bb2d8563422c553d1e08b39a09a873fae1ad0215bd1fcc0dbcaa69e62eab9332530717a200a52295018a578e3c615e83d68d84564060402'
       'SKIP'
       '44fb5119210dce11465934b15c2e2ca410176d81e0b5d1be72e303b93fdf735666979ac96873a0d1d1f5906f90fd3c5d665f25d2a45dba1fcf968023387c6432'
       'ec0ec031f4adc88f0053d3808a3e841bbed6921bf48af7f2f4719bbeffd952c463d914b1e2dfc31b38e170b6c9eb5285d4f6ae44090fe113df66f71c24c53450'
       'b83517c6441800a6570f4c06db5f6cb21e9a538b2c43cdaf1610d532ab5fd4196bd13f755589ec42832ff625bd2362600c6ad50595dcc5afb4fdcf488ae2438e'
       )
validpgpkeys=('0392335A78083894A4301C43236E8A58C6DB4512') # Max Kellermann <max@blarg.de>

build() {
  cd "${pkgname}-${pkgver}"
  meson --prefix=/usr \
        --buildtype=plain \
        build
  ninja -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  ninja -C build test
}

package() {
  cd "${pkgname}-${pkgver}"
  DESTDIR="${pkgdir}" ninja -C build install
  install -vDm 644 "doc/${pkgname}.conf" -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 "doc/${pkgname}.1" -t "${pkgdir}/usr/share/man/man1/"

  install -vDm 644 "build/systemd/system/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}@.service"
  install -vDm 644 "build/systemd/user/${pkgname}.service" "${pkgdir}/usr/lib/systemd/user/${pkgname}.service"

  install -vDm 644 "../${pkgname}.conf" -t "${pkgdir}/etc/"
  install -vDm 644 "../${pkgname}.sysusers" "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -vDm 644 "../${pkgname}.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}
