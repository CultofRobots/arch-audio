# Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://github.com/CultofRobots
# Based on PKGBUILD from AUR
# Contributor: jason ryan <jasonwryan [at] gmail [dot] com>
# Contributor: Bart≈Çomiej Piotrowski <nospam [at] bpiotrowwski [dot] pl>
# Contributor: Thomas Dziedzic <gostrc [at] gmail [dot] com>
# Contributor: evr <evanroman [at] gmail [dot] com>
# Contributor: Luiz Ribeiro <luizribeiro [at] gmail [dot] com>

pkgname=mpdscribble
pkgver=0.22
pkgrel=14
pkgdesc='MPD client which submits track info to {Libre,Last}.fm'
url='https://github.com/MusicPlayerDaemon/mpdscribble'
arch=('x86_64' 'armv7h' 'aarch64')
license=('GPL')
depends=('libsoup' 'glib2' 'libmpdclient')
install=$pkgname.install
source=(https://github.com/MusicPlayerDaemon/${pkgname}/archive/v${pkgver}.tar.gz
        service)
md5sums=('15296ef0e7207ca291ad31aa0c3c7f54'
         'a57d7d3d41b37fb23b45835aa0cfc325')

prepare() {
  sed 's/multi-user.target/default.target/;/User=%i/d' service > user.service

  cd ${pkgname}-"${pkgver}"
  autoreconf -i
}

build() {
  cd ${pkgname}-"${pkgver}"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-http-client=soup

  make
}

package() {
  cd ${pkgname}-"${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/service "${pkgdir}"/usr/lib/systemd/system/mpdscribble@.service
  install -Dm644 "${srcdir}"/user.service "${pkgdir}"/usr/lib/systemd/user/mpdscribble.service

  # default config is really an example
  install -D -m644 "${pkgdir}"/etc/mpdscribble.conf "${pkgdir}"/usr/share/mpdscribble/mpdscribble.conf.example
  rm -f "${pkgdir}"/etc/mpdscribble.conf

  install -d "${pkgdir}"/var/cache/mpdscribble
  touch "${pkgdir}"/var/cache/mpdscribble/mpdscribble.cache
}

