# Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://github.com/CultofRobots
# Based on PKGBUILD from AUR
# Contributor: jason ryan <jasonwryan [at] gmail [dot] com>
# Contributor: Bart≈Çomiej Piotrowski <nospam [at] bpiotrowwski [dot] pl>
# Contributor: Thomas Dziedzic <gostrc [at] gmail [dot] com>
# Contributor: evr <evanroman [at] gmail [dot] com>
# Contributor: Luiz Ribeiro <luizribeiro [at] gmail [dot] com>

pkgname=mpdscribble
pkgver=0.24
pkgrel=1
pkgdesc='MPD client which submits track info to {Libre,Last}.fm'
url="http://www.musicpd.org/"
arch=('x86_64' 'armv7h' 'aarch64')
license=('GPL2')
depends=('curl' 'libgcrypt' 'libmpdclient')
makedepends=('boost' 'systemd' 'meson' 'ninja')
backup=("etc/${pkgname}.conf")
source=("https://www.musicpd.org/download/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.sig}
        "${pkgname}.sysusers"
        "${pkgname}.tmpfiles"
        "${pkgname}.install")
sha512sums=('cca57f54f69ec1c870805cc58057e6325b6f5897370c315dcd6a64c5dd28ac07fdbddaa8b56f8c1fd88e71d27505f1a87decd68fbdff1f3fe1eaadec8ae80dae'
            'SKIP'
            '0f5b302ef3124a061a84fdd3dd8dd1a08a31bc99703d7dc32413fe499ea9613babf915f89926578019d8b6a2414e772022203de962bf971de8081af5b07d09c7'
            '67d6f5bfd70f3d949bd3d04bee93e6953ca3b1e50e71b2bc94f359c8abf385537424c3c386ba1d6cbe808b6e4498c4cfd191251076e4df0394a66bcc57ef2c48'
            'f3780fa6abccb7bbf0eec1fe61f47a4b2c0f25db65bb00da747df105eda1c31d6af628dbb4e8311e2302b4fb64ccac604fdc53c991af3e41048df107d65ab595'
           )
b2sums=('84143ed7f6978aff32fa759bbe90c934ba5c58df294d375921ffb1cd45de13ecd867fb832940088228fdad4c09948cd82c8dfe9b7fe77d6652743d522bfde8b7'
        'SKIP'
        'ec0ec031f4adc88f0053d3808a3e841bbed6921bf48af7f2f4719bbeffd952c463d914b1e2dfc31b38e170b6c9eb5285d4f6ae44090fe113df66f71c24c53450'
        'b83517c6441800a6570f4c06db5f6cb21e9a538b2c43cdaf1610d532ab5fd4196bd13f755589ec42832ff625bd2362600c6ad50595dcc5afb4fdcf488ae2438e'
        '0a7f49a074133b55e4a7b42734ca25078960f78a41b446c0c845e2a94587c9ed7038f55abd273501dcba4fc2a0b9633ceeda2c7eba034c5d10a6ad0a447602ab'
       )
validpgpkeys=('0392335A78083894A4301C43236E8A58C6DB4512') # Max Kellermann <max@blarg.de>

build() {
  cd "${pkgname}-${pkgver}"
  meson --prefix=/usr \
        --buildtype=plain \
        build
  ninja -C build
}

package() {
  cd "${pkgname}"-"${pkgver}"
  DESTDIR="${pkgdir}" meson install -C build
  
  install -vDm 644 build/systemd/system/"${pkgname}".service "${pkgdir}"/usr/lib/systemd/system/"${pkgname}"@.service
  install -vDm 644 build/systemd/user/"${pkgname}".service "${pkgdir}"/usr/lib/systemd/user/"${pkgname}".service

  # example config
  install -Dm644 doc/"${pkgname}".conf "${pkgdir}"/usr/share/"${pkgname}"/"${pkgname}".conf.example
  rm -r "${pkgdir}"/etc

  # man page
  install -vDm 644 "doc/${pkgname}.1" -t "${pkgdir}/usr/share/man/man1/"

  install -vDm 644 "../${pkgname}.sysusers" "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -vDm 644 "../${pkgname}.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}
