# Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Contributor: Nissar Chababy <funilrys at outlook dot com>
# Contributor: Celestial Walrus <aur at celestial dot cf>

pkgname=cava
pkgver=0.9.1
pkgrel=1
pkgdesc='Console-based Audio Visualizer for Alsa'
arch=('x86_64' 'armv7h' 'aarch64')
url='https://github.com/karlstav/cava'
license=('MIT')
depends=('fftw' 'alsa-lib' 'ncurses' 'iniparser' 'portaudio')
makedepends=('automake' 'autoconf')
optdepends=('sndio' 'pulseaudio')
source=("https://github.com/karlstav/cava/archive/${pkgver}.tar.gz")
sha512sums=('21af220b53a2f45c8c43dfd2ba47c1c96a8fcccdbd11ca1ac59eea7a4f32c0e27c7d2056281d0293fd17d4d635b1c568c953567d6f0301c800b4a387332d2a7a')

build() {
  cd ${pkgname}-${pkgver}
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 cava "$pkgdir/usr/bin/cava"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"${pkgname}"/LICENSE
}
