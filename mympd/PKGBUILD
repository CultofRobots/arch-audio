# Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://github.com/CultofRobots
# Based on Juergen Mang's myMPD PKGBUILD from https://github.com/jcorporation/myMPD
# Contributor: Juergen Mang <mail [at] jcgames [dot] de>

pkgname=mympd
_pkgname=myMPD
pkgver=7.0.2
pkgrel=1
pkgdesc="A standalone and mobile friendly web-based MPD client."
arch=('x86_64' 'armv7h' 'aarch64')
url="https://jcorporation.github.io/myMPD/"
license=('GPL2')
depends=(pcre)
makedepends=('cmake' 'perl')
optdepends=('openssl' 'libid3tag' 'flac' 'lua')
provides=()
conflicts=()
replaces=()
backup=('etc/webapps/mympd/mympd.conf')
install=mympd.install
source=("https://github.com/jcorporation/${_pkgname}/archive/v${pkgver}.tar.gz"
        'mympd.install'
        'mympd.sysusers')
sha256sums=('c8fd9f6e03243bd7a9318a9ed69ec03b109850894ab47503746fac4606de26c0'
            'f32bfd3959be6ffbad555ea439d920f11d9293b104e87437b90093f794f750b3'
            '82894a939c6adbfd5c719615adefb9a54ded1c221dbbd12b9a46cd90e1be8d8c')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}" || exit 1
  ./build.sh createassets
  cd release || exit 1
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_BUILD_TYPE=RELEASE ..
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}/release"

  install -D -m 644 "${srcdir}/mympd.sysusers" "${pkgdir}/usr/lib/sysusers.d/mympd.conf"
  install -dm755 "$pkgdir"/var/lib/${pkgname}

  make DESTDIR="$pkgdir/" install
}
