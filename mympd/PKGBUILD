# Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://github.com/CultofRobots
# Based on Juergen Mang's myMPD PKGBUILD from https://github.com/jcorporation/myMPD
# Contributor: Juergen Mang <mail [at] jcgames [dot] de>

pkgname=mympd
_pkgname=myMPD
pkgver=8.0.1
pkgrel=1
pkgdesc="A standalone and mobile friendly web-based MPD client."
arch=('x86_64' 'armv7h' 'aarch64')
url="https://jcorporation.github.io/myMPD/"
license=('GPL2')
depends=('pcre' 'openssl' 'libid3tag' 'flac' 'lua')
makedepends=('cmake' 'perl')
optdepends=()
provides=()
conflicts=()
replaces=()
install=mympd.install
source=("https://github.com/jcorporation/${_pkgname}/archive/v${pkgver}.tar.gz"
        'mympd.install'
        'mympd.sysusers')
sha256sums=('94f6fc50439a597013159ff387d7279616fead1fd72255134d96d6f2a47c5185'
            '7c126fd0c8bccebdf0b3359751296953828533e16187df33611686e5e40b5cb5'
            '82894a939c6adbfd5c719615adefb9a54ded1c221dbbd12b9a46cd90e1be8d8c')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}" || exit 1
  ./build.sh createassets
  cd release || exit 1
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_BUILD_TYPE=RELEASE ..
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}/release"

  install -D -m 644 "${srcdir}/mympd.sysusers" "${pkgdir}/usr/lib/sysusers.d/mympd.conf"
  install -dm755 "$pkgdir"/var/lib/${pkgname}

  make DESTDIR="$pkgdir/" install
}
