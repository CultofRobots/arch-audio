# Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://github.com/CultofRobots
# Based on Juergen Mang's myMPD PKGBUILD from https://github.com/jcorporation/myMPD
# Contributor: Juergen Mang <mail [at] jcgames [dot] de>

pkgname=mympd
_pkgname=myMPD
pkgver=6.4.2
pkgrel=1
pkgdesc="A standalone and mobile friendly web mpdclient."
arch=('x86_64' 'armv7h' 'aarch64')
url="http://github.org/jcorporation/myMPD"
license=('GPL')
depends=('openssl' 'libid3tag' 'flac')
makedepends=('cmake')
optdepends=()
provides=()
conflicts=()
replaces=()
backup=('etc/webapps/mympd/mympd.conf')
install=mympd.install
source=("https://github.com/jcorporation/${_pkgname}/archive/v${pkgver}.tar.gz"
        'mympd.install'
        'mympd.sysusers')
sha256sums=('9299e59bacbf4e51f47c32537328c4d9bc4d173fe03a94eb30fab0a1ee028a4c'
            'f32bfd3959be6ffbad555ea439d920f11d9293b104e87437b90093f794f750b3'
            '82894a939c6adbfd5c719615adefb9a54ded1c221dbbd12b9a46cd90e1be8d8c')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}" || exit 1
  install -d release
  cd release || exit 1
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_BUILD_TYPE=RELEASE ..
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}/release"

  install -D -m 644 "${srcdir}/mympd.sysusers" "${pkgdir}/usr/lib/sysusers.d/mympd.conf"
  install -dm755 "$pkgdir"/var/lib/${pkgname}

  make DESTDIR="$pkgdir/" install
}
