# Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://github.com/CultofRobots
# Based on Juergen Mang's myMPD PKGBUILD from https://github.com/jcorporation/myMPD
# Contributor: Juergen Mang <mail [at] jcgames [dot] de>

pkgname=mympd
_pkgname=myMPD
pkgver=9.3.1
pkgrel=1
pkgdesc="A standalone and mobile friendly web-based MPD client."
arch=('x86_64' 'armv7h' 'aarch64')
url="https://jcorporation.github.io/myMPD/"
license=('GPL3')
depends=('pcre2' 'openssl' 'libid3tag' 'flac' 'lua')
makedepends=('cmake' 'perl')
optdepends=()
provides=()
conflicts=()
replaces=()
install=${pkgname}.install
source=("https://github.com/jcorporation/${_pkgname}/archive/v${pkgver}.tar.gz"
        "${pkgname}.install"
        "${pkgname}.tmpfiles"
        "${pkgname}.sysusers")
sha256sums=('ebe658d254fb2ced58d4c5b5350f0afe671cb595f41f2faa16377064d664f94c'
            '2bf1eaae38740b3fd2c9dde574a8cba84e9b08c15e2f2468bcb4ecad2b9c0f11'
            '3e291b4ef2965594f0d972dd686fcc416b8beff428948bfda97d2972c4decab4'
            '82894a939c6adbfd5c719615adefb9a54ded1c221dbbd12b9a46cd90e1be8d8c')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}" || exit 1
  install -d release
  ./build.sh createassets
  cd release || exit 1
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_BUILD_TYPE=RELEASE ..
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}/release"
  make DESTDIR="$pkgdir/" install

  install -Dm644 "${srcdir}/${pkgname}.sysusers" "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -Dm644 "${srcdir}/${pkgname}.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"

}
