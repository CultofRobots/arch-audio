# Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://github.com/CultofRobots/
# Based on official arch linux mpd PKGBUILD made by the following people:
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Contributor: Ben <ben@benmazer.net>

pkgname=mpd-essential
_pkgname=mpd
pkgver=0.23.17
pkgrel=2
pkgdesc="Flexible, powerful, server-side application for playing music built with essential components."
arch=(x86_64 armv7h aarch64)
provides=("mpd=$pkgver")
conflicts=(mpd)
replaces=(mpd)
url="http://www.musicpd.org/"
license=(
  BSD-2-Clause
  GPL-2.0-or-later
)
depends=(
  gcc-libs
  glibc
  libmad
  libshout
  libsoxr
  wavpack
  zlib
)
makedepends=(
  alsa-lib
  avahi
  boost
  curl
  dbus
  expat
  faad2
  ffmpeg
  flac
  fmt
  git
  icu
  lame
  libao
  libid3tag
  libmpdclient
  libogg
  libopenmpt
  libupnp
  liburing
  libvorbis
  meson
  opus
  python-sphinx
  sqlite
  systemd
  yajl
)
backup=("etc/$_pkgname.conf")
source=(
  $_pkgname::git+https://github.com/MusicPlayerDaemon/MPD.git#tag=v${pkgver}?signed
  $_pkgname.conf
  $_pkgname.sysusers
  $_pkgname.tmpfiles
  $_pkgname.service.override
)
sha512sums=('6c156fd543f426dd7927dac4871d0fc00c3766e55447a800c1987a9a0652384bfddad56aaff8c5a4c3c9ac5e537492dbc67b64a7f493f9b85c17145f901f5c1c'
            'e1f2cede2aae5500bc473c8a95df28134015fa99b6af781bfe2b5ef57f4624fd4e2e4477d690a6147becab9079934cfe42f1603daa69930a0d9e97393f6f167b'
            'd66c1d771160ee1781a05e57f383acc466babb29924c07d83ac0e763c14380dd1f279ba7b4aec508dc70245370d9732b4bc6287df1a2e06a920f3b73551d3032'
            'db473db27cd68994c3ee26e78e0fb34d13126301d8861563dcc12a22d62ecb14c4ffb1e0798c6aaccdff34e73bae3fbeeff7b42606c901a2d35e278865cdf35d'
            'c1782b82f9db1d30aece43a07230c5d57370f2494a16e108af03815d83968805472f10f53ea5495cf0e08ff8f245430c3c3bc44025af43aaf9ecd12fcd6afc6c')
b2sums=('25365047cd8238fbcd2adc0cf4ae28c1403810b55939bf106f0139e75341c065c788f288eebfffe515264150ba5410bf25cf98ae03514f0d3038ae95fb451e83'
        '77102efd67d46f6887057aee0fd3b19582b08219be7b0aa8db47f0094e1f062fbd8eb98c9f4c9ea35be7823653c54280bc5f62e62a4eb62321e9c12507acaeaf'
        '814c2314de6040e895657a8c8d62f11bc38c224a3c0ef5cbf280c0e141c80f04b0ac5026be06fd5dc4a4b764f3d91ab46f365da0a7bd466abc3aed02b0612165'
        'd7b587c25dd5830c27af475a8fdd8102139d7c8fdd6f04fe23b36be030e4411582e289f575c299255ff8183096f7d47247327276f9a24641cbd032d9675b837a'
        '753664445d7d5cc0b36f51ac66549beea403b9731cbcb81b0a782974a0a73d90559ba93e6afcaa470b6f2f5a844c09ef695bdf3b1e6dfee97aa080f41b7fe513')
validpgpkeys=('0392335A78083894A4301C43236E8A58C6DB4512') # Max Kellermann <max@blarg.de>

build() {
  local _meson_options=(
    -D documentation=enabled
    -D adplug=disabled
    -D audiofile=disabled
    -D sndio=disabled
    -D shine=disabled
    -D tremor=disabled
    -D b_ndebug=true
    -D chromaprint=disabled
    -D ipv6=disabled
    -D webdav=disabled
    -D cdio_paranoia=disabled
    -D mms=disabled
    -D nfs=disabled
    -D smbclient=disabled
    -D qobuz=disabled
    -D bzip2=disabled
    -D iso9660=disabled
    -D zzip=disabled
    -D fluidsynth=disabled
    -D gme=disabled
    -D mikmod=disabled
    -D modplug=disabled
    -D mpcdec=disabled
    -D mpg123=disabled
    -D sidplay=disabled
    -D sndfile=disabled
    -D wildmidi=disabled
    -D twolame=disabled
    -D libsamplerate=disabled
    -D ao=disabled
    -D jack=disabled
    -D openal=disabled
    -D oss=disabled
    -D pipewire=disabled
    -D pulse=disabled
    -D recorder=false
  )

  # NOTE: sndio conflicts with alsa
  # TODO: package adplug
  # TODO: package shine
  arch-meson "${_meson_options[@]}" build $_pkgname
  ninja -C build
}

check() {
  ninja -C build test
}

package() {
  depends+=(
    alsa-lib libasound.so
    avahi libavahi-{client,common}.so
    curl libcurl.so
    dbus libdbus-1.so
    expat libexpat.so
    faad2 libfaad.so
    ffmpeg libav{codec,filter,format,util}.so
    flac libFLAC.so
    fmt libfmt.so
    icu libicui18n.so libicuuc.so
    lame libmp3lame.so
    libao libao.so
    libid3tag libid3tag.so
    libmpdclient libmpdclient.so
    libogg libogg.so
    libopenmpt libopenmpt.so
    libupnp libixml.so libupnp.so
    liburing liburing.so
    libvorbis libvorbis{,enc}.so
    opus libopus.so
    sqlite libsqlite3.so
    systemd-libs libsystemd.so
    yajl libyajl.so
  )

  DESTDIR="$pkgdir" ninja -C build install
  install -vDm 644 $_pkgname/doc/${_pkgname}conf.example -t "$pkgdir/usr/share/doc/$_pkgname/"
  # NOTE: BSD-2-Clause license file currently missing: https://github.com/MusicPlayerDaemon/MPD/issues/1877
  # install -vDm 644 $_pkgname-$pkgver/LICENSES/BSD-2-Clause -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $_pkgname.service.override "$pkgdir/usr/lib/systemd/system/mpd.service.d/00-arch.conf"
  install -vDm 644 $_pkgname.conf -t "$pkgdir/etc/"
  install -vDm 644 $_pkgname.sysusers "$pkgdir/usr/lib/sysusers.d/$_pkgname.conf"
  install -vDm 644 $_pkgname.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/$_pkgname.conf"
}
