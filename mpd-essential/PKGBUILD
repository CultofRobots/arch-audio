# Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://github.com/CultofRobots/
# Based on official arch linux mpd PKGBUILD made by the following people:
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Contributor: Ben <ben@benmazer.net>

pkgname=mpd-essential
_pkgname=mpd
pkgver=0.23.14
pkgrel=1
pkgdesc="Flexible, powerful, server-side application for playing music built with essential components."
arch=(x86_64 armv7h aarch64)
provides=("mpd=$pkgver")
conflicts=(mpd)
replaces=(mpd)
url="http://www.musicpd.org/"
# NOTE: BSD-2-Clause license file currently missing: https://github.com/MusicPlayerDaemon/MPD/issues/1877
license=(
  BSD-2-Clause
  GPL-2.0-or-later
)
depends=(
  gcc-libs
  glibc
  libmad
  libshout
  libsoxr
  wavpack
  zlib
)
makedepends=(
  alsa-lib
  audiofile
  avahi
  boost
  curl
  dbus
  expat
  faad2
  ffmpeg
  flac
  fmt
  icu
  lame
  libid3tag
  libmpdclient
  libogg
  libopenmpt
  libupnp
  liburing
  libvorbis
  meson
  opus
  python-sphinx
  sqlite
  systemd
  yajl
)
backup=("etc/$_pkgname.conf")
source=(
  https://www.musicpd.org/download/$_pkgname/${pkgver%.*}/$_pkgname-${pkgver}.tar.xz{,.sig}
  $_pkgname.conf
  $_pkgname.sysusers
  $_pkgname.tmpfiles
  $_pkgname.service.override
)
sha512sums=('4d97b22c37ca8c0939830cd6497023d868005a123f866db594716027c590e2d1dffb65bbd3af41404dcc7c0249466192e457ae1ba80f1f95ff044516be4c99ae'
            'SKIP'
            'e1f2cede2aae5500bc473c8a95df28134015fa99b6af781bfe2b5ef57f4624fd4e2e4477d690a6147becab9079934cfe42f1603daa69930a0d9e97393f6f167b'
            '6e467481406279767b709ec6d5c06dbd825c0de09045c52ffa2d21d0604dcfe19b7a92bf42bed25163d66a3a0d1dbde6185a648b433eaf5eac56be90491e2e18'
            'db473db27cd68994c3ee26e78e0fb34d13126301d8861563dcc12a22d62ecb14c4ffb1e0798c6aaccdff34e73bae3fbeeff7b42606c901a2d35e278865cdf35d'
            'c1782b82f9db1d30aece43a07230c5d57370f2494a16e108af03815d83968805472f10f53ea5495cf0e08ff8f245430c3c3bc44025af43aaf9ecd12fcd6afc6c')
b2sums=('c048f128111d1d65775c317182b91d113339a5b09d3005c320cc3b14a79b7c1da0d1ba3d53f6bf348a3a404ceea33c1ad2427225f4a1f3d1cde4a921e71d6e1c'
        'SKIP'
        '77102efd67d46f6887057aee0fd3b19582b08219be7b0aa8db47f0094e1f062fbd8eb98c9f4c9ea35be7823653c54280bc5f62e62a4eb62321e9c12507acaeaf'
        '4ab6e415284c77802a39d0913d701fe55e56f3c22b19557661fbef77e456b5e1d151da4202695282b956602e716a7afdb994aa2fc17368b9a0d0d051d47a3afb'
        'd7b587c25dd5830c27af475a8fdd8102139d7c8fdd6f04fe23b36be030e4411582e289f575c299255ff8183096f7d47247327276f9a24641cbd032d9675b837a'
        '753664445d7d5cc0b36f51ac66549beea403b9731cbcb81b0a782974a0a73d90559ba93e6afcaa470b6f2f5a844c09ef695bdf3b1e6dfee97aa080f41b7fe513')
validpgpkeys=('0392335A78083894A4301C43236E8A58C6DB4512') # Max Kellermann <max@blarg.de>

build() {
  local _meson_options=(
    -D adplug=disabled
    -D sndio=disabled
    -D shine=disabled
    -D tremor=disabled
    -D b_ndebug=true
    -D chromaprint=disabled
    -D ipv6=disabled
    -D webdav=disabled
    -D cdio_paranoia=disabled
    -D mms=disabled
    -D nfs=disabled
    -D smbclient=disabled
    -D qobuz=disabled
    -D bzip2=disabled
    -D iso9660=disabled
    -D zzip=disabled
    -D fluidsynth=disabled
    -D gme=disabled
    -D mikmod=disabled
    -D modplug=disabled
    -D mpcdec=disabled
    -D mpg123=disabled
    -D sidplay=disabled
    -D sndfile=disabled
    -D wildmidi=disabled
    -D twolame=disabled
    -D libsamplerate=disabled
    -D ao=disabled
    -D jack=disabled
    -D openal=disabled
    -D oss=disabled
    -D pipewire=disabled
    -D pulse=disabled
    -D recorder=false
  )

  # NOTE: sndio conflicts with alsa
  # TODO: package adplug
  # TODO: package shine
  arch-meson "${_meson_options[@]}" build $_pkgname-$pkgver
  ninja -C build
}

check() {
  ninja -C build test
}

package() {
  depends+=(
    alsa-lib libasound.so
    audiofile libaudiofile.so
    avahi libavahi-{client,common}.so
    curl libcurl.so
    dbus libdbus-1.so
    expat libexpat.so
    faad2 libfaad.so
    ffmpeg libav{codec,filter,format,util}.so
    flac libFLAC.so
    fmt libfmt.so
    icu libicui18n.so libicuuc.so
    lame libmp3lame.so
    libid3tag libid3tag.so
    libmpdclient libmpdclient.so
    libogg libogg.so
    libopenmpt libopenmpt.so
    libupnp libixml.so libupnp.so
    liburing liburing.so
    libvorbis libvorbis{,enc}.so
    opus libopus.so
    sqlite libsqlite3.so
    systemd-libs libsystemd.so
    yajl libyajl.so
  )

  DESTDIR="$pkgdir" ninja -C build install
  install -vDm 644 $_pkgname-$pkgver/doc/${_pkgname}conf.example -t "$pkgdir/usr/share/doc/$_pkgname/"
  install -vDm 644 $_pkgname.service.override "$pkgdir/usr/lib/systemd/system/mpd.service.d/00-arch.conf"
  install -vDm 644 $_pkgname.conf -t "$pkgdir/etc/"
  install -vDm 644 $_pkgname.sysusers "$pkgdir/usr/lib/sysusers.d/$_pkgname.conf"
  install -vDm 644 $_pkgname.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/$_pkgname.conf"
}
